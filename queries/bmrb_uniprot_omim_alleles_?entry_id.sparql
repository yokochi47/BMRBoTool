PREFIX BMRBo: <https://bmrbpub.pdbj.org/schema/mmcif_nmr-star.owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX omim_v: <http://bio2rdf.org/omim_vocabulary:>

SELECT DISTINCT ?entity_id ?uniprot_id ?label ?omim_id ?dbsnp_id ?mutation ?phenotype
FROM <https://bmrbpub.pdbj.org/rdf/bmr>
FROM <http://purl.uniprot.org/uniprot>
FROM <http://bio2rdf.org/omim_resource:bio2rdf.dataset.omim.R3>
WHERE {

  SERVICE <https://bmrbpub.pdbj.org/search/rdf> {

    ?s_up BMRBo:entity_db_link.entry_id ?entry_id ; # fill entry_id.
          BMRBo:entity_db_link.entity_id ?entity_id ;
          BMRBo:entity_db_link.database_code "SP" ; # uniprot
          BMRBo:entity_db_link.accession_code ?uniprot_id ;
          rdfs:seeAlso ?s_uniprot .

    ?s_uniprot rdfs:label ?info .

    FILTER (STRSTARTS(?info, "info:uniprot"))

  }

  SERVICE <http://sparql.uniprot.org/sparql>
  {

    ?s_uniprot rdfs:label ?label ;
               rdfs:seeAlso ?o_purl .

  }

  FILTER (STRSTARTS(STR(?o_purl), "http://purl.uniprot.org/mim/"))

  BIND (STRAFTER(STR(?o_purl), "http://purl.uniprot.org/mim/") AS ?omim_id)
  BIND (IRI(CONCAT("http://bio2rdf.org/omim:", ?omim_id)) AS ?s_omim)

  SERVICE <http://omim.bio2rdf.org/sparql>
  {

    ?s_omim omim_v:variant ?s_allele .

    ?s_allele omim_v:x-dbsnp ?s_dbsnp ;
              omim_v:mutation ?mutation ;
              rdfs:label ?phenotype .

    BIND (STRAFTER(STR(?s_dbsnp), "http://bio2rdf.org/dbsnp:") AS ?dbsnp_id)

  }

}
