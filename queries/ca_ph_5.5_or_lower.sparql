PREFIX BMRBo: <https://bmrbpub.pdbj.org/schema/mmcif_nmr-star.owl#>

SELECT ?entry_id ?seq_id ?comp_id ?ca_ppm
FROM <https://bmrbpub.pdbj.org/rdf/bmr>
WHERE {

  ?s_ph BMRBo:sample_condition_variable.type "pH" ;
        BMRBo:sample_condition_variable.val ?ph ;
        BMRBo:sample_condition_variable.entry_id ?entry_id ;
        BMRBo:sample_condition_variable.sample_condition_list_id ?sample_condition_list_id .

  FILTER (xsd:float(?ph) > 0.0 && xsd:float(?ph) < 5.5)

  ?s_csl BMRBo:assigned_chem_shift_list.entry_id ?entry_id ;
         BMRBo:assigned_chem_shift_list.sample_condition_list_id ?sample_condition_list_id ;
         BMRBo:assigned_chem_shift_list.id ?assigned_chem_shift_list_id .

  ?s_ca BMRBo:atom_chem_shift.atom_id "CA" ;
        BMRBo:atom_chem_shift.atom_type "C" ;
        BMRBo:atom_chem_shift.entry_id ?entry_id ;
        BMRBo:atom_chem_shift.seq_id ?seq_id ;
        BMRBo:atom_chem_shift.comp_id ?comp_id ;
        BMRBo:atom_chem_shift.assigned_chem_shift_list_id ?assigned_chem_shift_list_id ;
        BMRBo:atom_chem_shift.val ?ca_ppm .

} ORDER BY ?entry_id
LIMIT 100
