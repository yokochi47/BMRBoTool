PREFIX PDBo: <http://rdf.wwpdb.org/schema/pdbx-v40.owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX omim_v: <http://bio2rdf.org/omim_vocabulary:>

SELECT DISTINCT ?pdb_id ?entity_id ?pdb_seq ?uniprot_id ?label ?uniprot_seq ?omim_id ?dbsnp_id ?mutation ?phenotype
FROM <http://rdf.wwpdb.org/pdb>
FROM <http://purl.uniprot.org/uniprot>
FROM <http://bio2rdf.org/omim_resource:bio2rdf.dataset.omim.R3>
WHERE {

  BIND ("4K75" AS ?pdb_id)

  BIND (IRI(CONCAT("http://rdf.wwpdb.org/pdb/", ?pdb_id, "/struct_refCategory")) AS ?s_pdb)

  SERVICE <http://bmrbpub.protein.osaka-u.ac.jp/search/rdf> {

    ?s_pdb PDBo:has_struct_ref ?o_struct_ref .

    ?o_struct_ref PDBo:struct_ref.db_name           "UNP" ;
                  PDBo:struct_ref.entity_id         ?entity_id ;
                  PDBo:struct_ref.pdbx_db_accession ?uniprot_id ;
                  PDBo:link_to_uniprot              ?s_uniprot ;
                  PDBo:struct_ref.pdbx_seq_one_letter_code ?uniprot_seq .
                  
  }

  BIND (IRI(CONCAT("http://rdf.wwpdb.org/pdb/", ?pdb_id, "/entity_poly/", ?entity_id)) AS ?s_pdb_entity_poly)

  SERVICE <http://bmrbpub.protein.osaka-u.ac.jp/search/rdf> {

    ?s_pdb_entity_poly PDBo:entity_poly.pdbx_seq_one_letter_code ?pdb_seq .

  }

  SERVICE <http://sparql.uniprot.org/sparql>
  {

    ?s_uniprot rdfs:label ?label .
               rdfs:seeAlso ?o_purl .

  }

  FILTER (STRSTARTS(STR(?o_purl), "http://purl.uniprot.org/mim/"))

  BIND (STRAFTER(STR(?o_purl), "http://purl.uniprot.org/mim/") AS ?omim_id)
  BIND (IRI(CONCAT("http://bio2rdf.org/omim:", ?omim_id)) AS ?s_omim)

  SERVICE <http://omim.bio2rdf.org/sparql>
  {

    ?s_omim omim_v:variant ?s_allele .

    ?s_allele omim_v:x-dbsnp ?s_dbsnp ;
              omim_v:mutation ?mutation ;
              rdfs:label ?phenotype .

    BIND (STRAFTER(STR(?s_dbsnp), "http://bio2rdf.org/dbsnp:") AS ?dbsnp_id)

  }

}
