PREFIX PDBo: <http://rdf.wwpdb.org/schema/pdbx-v50.owl#>

SELECT DISTINCT ?pdb_id ?entity_id ?pdb_seq ?uniprot_id ?label ?uniprot_seq
FROM <http://rdf.wwpdb.org/pdb>
FROM <http://purl.uniprot.org/uniprot>
WHERE {

  BIND ("2IFY" AS ?pdb_id)

  BIND (IRI(CONCAT("http://rdf.wwpdb.org/pdb/", ?pdb_id, "/struct_refCategory")) AS ?s_pdb)

  SERVICE <http://bmrbpub.pdbj.org/search/rdf> {

    ?s_pdb PDBo:has_struct_ref ?o_struct_ref .

    ?o_struct_ref PDBo:struct_ref.db_name           "UNP" ;
                  PDBo:struct_ref.entity_id         ?entity_id ;
                  PDBo:struct_ref.pdbx_db_accession ?uniprot_id ;
                  PDBo:link_to_uniprot              ?s_uniprot ;
                  PDBo:struct_ref.pdbx_seq_one_letter_code ?uniprot_seq .

  }

  BIND (IRI(CONCAT("http://rdf.wwpdb.org/pdb/", ?pdb_id, "/entity_poly/", ?entity_id)) AS ?s_pdb_entity_poly)

  SERVICE <http://bmrbpub.pdbj.org/search/rdf> {

    ?s_pdb_entity_poly PDBo:entity_poly.pdbx_seq_one_letter_code ?pdb_seq .

  }

  SERVICE <http://sparql.uniprot.org/sparql>
  {

    ?s_uniprot rdfs:label ?label .

  }

}
